plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.teamEWSN'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	mapstructVersion = "1.6.3"
	springDocVersion = "2.8.9"
	jjwtVersion = "0.12.6"
	awsSdkVersion = "2.25.68"
	springCloudAwsVersion = "3.1.1"
	queryDslVersion = "5.1.0"
}

dependencies {
	// Spring web
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Cache
	implementation 'org.springframework.boot:spring-boot-starter-cache'

	// AOP
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	// validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Spring Security
	implementation 'org.springframework.boot:spring-boot-starter-security'

	// OAuth 2.0
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// mapstruct
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

	// Swagger (SpringDoc)
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"

	// JWT
	implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"

	// dev-tools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// S3 bucket
//	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
//	implementation platform('software.amazon.awssdk:bom:2.20.0')
//	implementation 'software.amazon.awssdk:s3'
//	implementation 'software.amazon.awssdk:sts'
	implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:${springCloudAwsVersion}")
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'

	// íŒŒì¼ì˜ ë§¤ì§ ë°”ì´íŠ¸ ê²€ì‚¬ìš© Apache Tika ë¼ì´ë¸ŒëŸ¬ë¦¬
	implementation 'org.apache.tika:tika-core:3.2.2'

	// queryDsl
	implementation "com.querydsl:querydsl-core:${queryDslVersion}"
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
	annotationProcessor (
			"com.querydsl:querydsl-apt:${queryDslVersion}:jakarta",
			'jakarta.annotation:jakarta.annotation-api',
			// JPA ë©”íƒ€ëª¨ë¸ ìƒì„±ìš©
			'jakarta.persistence:jakarta.persistence-api'
	)

	// db
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'

	// Caffeine
	implementation 'com.github.ben-manes.caffeine:caffeine'

	// redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// Reactive ì›¹ í”„ë ˆì„ì›Œí¬ WebFlux
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// Email
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	// Github Webhook
	implementation 'org.kohsuke:github-api:1.327'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

/** Q í´ë˜ìŠ¤ ìƒì„± ê²½ë¡œ ì§€ì • **/
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

// gradle clean ì‹œì— QClass ë””ë ‰í† ë¦¬ ì‚­ì œ
clean {
	delete file(querydslDir)
}

// ğŸ”§ ì†ŒìŠ¤ì…‹ì— generated ë””ë ‰í† ë¦¬ ì¶”ê°€
sourceSets {
	main.java.srcDirs += [ querydslDir ]
}

tasks.withType(JavaCompile).configureEach {
	options.generatedSourceOutputDirectory.set(querydslDir)
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
}
